/*
 * Copyright (c) 2020 Adventech <info@adventech.io>
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 29
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1

        versionProps['VERSION_BUILD']=versionBuild.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.cryart.sabbathschool"
            minSdkVersion 16
            targetSdkVersion 29
            versionCode versionBuild
            versionName "3.4." + versionBuild

            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

            multiDexEnabled true

            buildConfigField "String", "API_BASE_URL", '"https://sabbath-school.adventech.io/"'
        }

    } else {
        throw new GradleException("Could not read version.properties!")
    }

    dexOptions {
        jumboMode = true
    }

    Properties keyProps = new Properties()
    keyProps.load(new FileInputStream(file("../keys/keystore.properties")))

    signingConfigs {
        release {
            storeFile file(keyProps['release.keystore'])
            storePassword keyProps['release.keystore.password']
            keyAlias keyProps['key.alias']
            keyPassword keyProps['key.password']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'

    androidTestImplementation "tools.fastlane:screengrab:1.1.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"

    implementation "androidx.multidex:multidex:${versions.multidex}"
    implementation 'androidx.appcompat:appcompat:1.2.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha06'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation "androidx.constraintlayout:constraintlayout:${versions.const_layout}"
    implementation 'androidx.core:core-ktx:1.3.0-rc01'
    implementation "androidx.activity:activity-ktx:1.2.0-alpha04"
    implementation "androidx.fragment:fragment-ktx:1.3.0-alpha04"
    implementation 'androidx.browser:browser:1.2.0'

    // Firebase
    implementation "com.google.android.gms:play-services-auth:18.0.0"
    implementation "com.google.firebase:firebase-core:${versions.fb_core}"
    implementation "com.google.firebase:firebase-analytics:${versions.fb_analytics}"
    implementation "com.google.firebase:firebase-auth:${versions.fb_auth}"
    implementation "com.google.firebase:firebase-database:${versions.fb_database}"
    implementation "com.google.firebase:firebase-storage:${versions.fb_storage}"
    implementation "com.google.firebase:firebase-storage-ktx:${versions.fb_storage}"
    implementation "com.google.firebase:firebase-messaging:${versions.fb_msg}"
    implementation "com.google.firebase:firebase-crashlytics:${versions.fb_crashlytics}"
    implementation "com.firebase:firebase-jobdispatcher:0.8.5"

    implementation "uk.co.samuelwall:material-tap-target-prompt:2.0.1"
    implementation "com.nostra13.universalimageloader:universal-image-loader:1.9.5"
    implementation "com.github.hotchemi:android-rate:1.0.1"
    implementation "org.apache.commons:commons-lang3:3.7"
    implementation "com.afollestad.material-dialogs:core:0.9.6.0"
    implementation "net.danlew:android.joda:2.10.3"
    implementation "com.facebook.android:facebook-android-sdk:4.23.0"
    implementation "com.mikepenz:iconics-core:2.8.1@aar"
    implementation "com.mikepenz:google-material-typeface:2.2.0.3.original@aar"
    implementation "com.github.pwittchen:reactivenetwork:0.8.0"
    implementation "net.opacapp:multiline-collapsingtoolbar:1.4.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:${versions.glide}"
    kapt "com.github.bumptech.glide:compiler:${versions.glide}"

    // Dagger
    implementation "com.google.dagger:dagger-android:${versions.dagger}"
    implementation "com.google.dagger:dagger-android-support:${versions.dagger}"
    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${versions.dagger}"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:${versions.coroutines}"

    // Retrofit
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp3}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp3}"
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"

    implementation "com.jakewharton.timber:timber:${versions.timber}"
}

apply plugin: 'com.google.gms.google-services'

play {
    serviceAccountCredentials = file("../keys/play_store_key.json")
    defaultToAppBundles = true
}
